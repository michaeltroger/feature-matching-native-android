apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion = 30
    buildToolsVersion = '30.0.3'
    // if you want to use the latest ndk (compilation with libc++ instead of gnustl) then 3 steps are required:
    // 1. set ndk version to 23.0.7599858
    // 2. set minSdkVersion to 24
    // 3. in CMakeLists.txt change the file path of the opencv lib to
    //     ${lib_src_dir}/native/libsselfcompiled/${ANDROID_ABI}/libopencv_java3.so
    ndkVersion = '16.1.4479499' // 23.0.7599858 when using latest ndk

    defaultConfig {
        applicationId = 'com.michaeltroger.featureMatchingNative'
        minSdkVersion 14 //24 when using latest ndk
        targetSdkVersion 30
        versionCode = 3
        versionName = '1.2'
        ndk.abiFilters 'x86', 'armeabi-v7a'
        //only x86 abis specified here
        //a workaround with build variants was required to allow x64 support too
        //the issue was that the custom c++ code was not compiled and copied into the
        //x64 apks when minsdk set to 14. x64 is supported since minsdk 21 only
        externalNativeBuild {
            cmake {
                cppFlags "-fexceptions", "-frtti"
            }
        }
    }
    flavorDimensions "api"
    productFlavors {
        minApi14 {  // default variant with only x86 support but works with all devices
            dimension "api"
        }
        minApi21 { //choose this build variant to get x64 support (recommended on modern devices)
            dimension "api"
            minSdkVersion 21
            ndk.abiFilters += 'x86_64'
            ndk.abiFilters += 'arm64-v8a'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}


dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.activity:activity-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.google.android.material:material:1.4.0'

    implementation project(':opencv-3-4-15')
}
repositories {
    mavenCentral()
}
